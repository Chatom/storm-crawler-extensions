apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'
group = 'com.shopstyle.storm-crawler-extensions'
version = '0.4-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    provided {
        description = 'Dependencies solely needed at compile time'
        transitive = false
    }

    compile {
        extendsFrom provided
    }
}

dependencies {
    provided (
        [group:'com.digitalpebble',          name:'storm-crawler-core',     version:'0.5-SNAPSHOT']
    )

    compile (
        [group:'org.apache.any23',           name:'apache-any23-core',      version:'1.1',          transitive:false],
        [group:'org.apache.commons',         name:'commons-lang3',          version:'3.3.2'],
        [group:'org.slf4j',                  name:'slf4j-api',              version:'1.7.7']

    )

    testCompile (
        [group:'commons-io',                 name:'commons-io',             version:'2.4'],
        [group:'junit',                      name:'junit',                  version:'4.11'],
        [group:'net.sourceforge.nekohtml',   name:'nekohtml',               version:'1.9.19'],
        [group:'org.mockito',                name:'mockito-all',            version:'1.9.5']
    )
}

eclipse {
    classpath {
        // Add the 'provided' (aka compile-only) configuration into the Eclipse classpath
        plusConfigurations += [ configurations.provided ]
    }
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
    from sourceSets.main.java
    classifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            // By default, the Gradle plugin publishes all artifacts
            // with scope='runtime'... In other words, we have to edit
            // the generated POM by hand pretty much...
            pom.withXml { xml ->
                xml.asNode().dependencies.dependency.each{
                   def groupId = it.groupId.get(0).text();
                   println groupId;
                   switch (groupId) {
                       case 'com.digitalpebble':
                           it.scope.get(0).value = 'provided';
                           break;
                       case 'org.apache.any23':
                           it.scope.get(0).value = 'compile';
                           def exclusion = it.appendNode('exclusions').appendNode('exclusion');
                           exclusion.appendNode('groupId').value = '*'
                           exclusion.appendNode('artifactId').value = '*'
                           break;
                       default:
                           it.scope.get(0).value = 'compile';
                   }
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "publisher"
                password System.properties['pub.pwd']
            }
            // change to point to your repo, e.g. http://my.org/repo
            url "https://maven.sugarops.com/repository/" + System.properties['pub.repoName']
        }
    }
}
